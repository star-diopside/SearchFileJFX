plugins {
    id "org.springframework.boot" version "2.3.2.RELEASE" apply false
    id "org.openjfx.javafxplugin" version "0.0.9" apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    group = "jp.gr.java_conf.stardiopside.searchfile.javafx"

    ext {
        javafxVersion = "14.0.2.1"
        controlsfxVersion = "11.0.2"
    }

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

project(":searchfile") {
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "application"

    mainClassName = "jp.gr.java_conf.stardiopside.searchfile.javafx.SearchFile"

    javafx {
        version = javafxVersion
        modules = [ "javafx.controls", "javafx.fxml" ]
    }

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter"
        implementation "org.controlsfx:controlsfx:${controlsfxVersion}"
    }
}

project(":searchfile-boot") {
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "jp.gr.java_conf.stardiopside.searchfile.javafx.SearchFile"
    }

    dependencies {
        runtimeOnly project(":searchfile")
    }
}
